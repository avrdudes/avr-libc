#                                               -*- Autoconf -*-
#
# Copyright (c) 2004,  Theodore A. Roth
# Copyright (c) 2005,2006,2007,2009  Anatoly Sokolov
# Copyright (c) 2005,2008  Joerg Wunsch
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# * Redistributions of source code must retain the above copyright
#   notice, this list of conditions and the following disclaimer.
# * Redistributions in binary form must reproduce the above copyright
#   notice, this list of conditions and the following disclaimer in
#   the documentation and/or other materials provided with the
#   distribution.
# * Neither the name of the copyright holders nor the names of
#   contributors may be used to endorse or promote products derived
#   from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Process this file with autoconf to produce a configure script.


dnl library versioning -- modify these on releases
dnl        macro name           value            description
m4_define([avr_libc_major],    [2])dnl           major version
m4_define([avr_libc_minor],    [3])dnl           minor version
m4_define([avr_libc_revision], [0])dnl           revision (dot-dot version)
m4_define([avr_libc_revision_suffix], [git])dnl  revision suffix (if any)
m4_define([avr_libc_reldate],  [20240609])dnl    release date
dnl end of library versioning data

m4_define([avr_libc_version],
          avr_libc_major.avr_libc_minor.avr_libc_revision()avr_libc_revision_suffix)dnl
m4_define([avr_libc_version_numeric],
          m4_eval(10000 * avr_libc_major + dnl
                    100 * avr_libc_minor + dnl
                          avr_libc_revision))dnl

AC_INIT([avr-libc], [avr_libc_version],
	[https://github.com/avrdudes/avr-libc/issues])

AC_PREREQ([2.59])
AC_REVISION($Revision$)

AC_CONFIG_SRCDIR([doc/examples/demo/demo.c])
AC_CONFIG_HEADERS([config.h])

dnl Makefile.in substitutions for M4 macros.
AC_SUBST(AVR_LIBC_MAJOR, [avr_libc_major])
AC_SUBST(AVR_LIBC_MINOR, [avr_libc_minor])
AC_SUBST(AVR_LIBC_REVISION, [avr_libc_revision])
AC_SUBST(AVR_LIBC_RELDATE, [avr_libc_reldate])
AC_SUBST(AVR_LIBC_VERSION, [avr_libc_version])
AC_SUBST(AVR_LIBC_VERSION_NUMERIC, [avr_libc_version_numeric])

AC_CANONICAL_BUILD
AC_CANONICAL_HOST

# See if the user specified '--build=`../config.guess` --host=avr' when
# running ../configure.

AC_MSG_CHECKING([if configuring for cross compile])
if test "x${build_alias}" = "x${host_alias}"; then
	if test "x${build_alias}" = "x" ; then
		build_alias='`./config.guess`'
	fi
	AC_MSG_RESULT(no)
	AC_MSG_WARN()
	AC_MSG_WARN(AVR-LibC must be built using an avr cross-compiler.)
	AC_MSG_WARN(Try configuring with:)
	AC_MSG_WARN("$0 --build=$build_alias --host=avr")
	AC_MSG_WARN()
	AC_MSG_ERROR(aborting configure)
else
	AC_MSG_RESULT([yes])
fi

AC_MSG_CHECKING([if target host is avr])
case "$host" in
	avr* )
		AC_MSG_RESULT([yes])
		;;
	* )
		AC_MSG_RESULT(no)
		AC_MSG_WARN()
		AC_MSG_WARN(AVR-LibC must be built using an avr cross-compiler.)
		AC_MSG_WARN(Try configuring with:)
		AC_MSG_WARN("$0 --build=$build --host=avr")
		AC_MSG_WARN()
		AC_MSG_ERROR(aborting configure)
		;;
esac

## TODO: Write a check for GNU Make

dnl The default check whether the C compiler can create an executable
dnl is inappropriate for us as it requires an existing library.
AC_NO_EXECUTABLES

dnl Minimum version of Automake is 1.8.
dnl We don't want the gzip distribution tarball anymore.
AM_INIT_AUTOMAKE([1.8 dist-bzip2 no-dist-gzip])

dnl m4 files that are hand-written.
dnl Macros that are required by m4-gen are in m4/mlib-*.m4.
AC_CONFIG_MACRO_DIRS([m4])

dnl m4 files generated by devtools/mlib-gen.py
AC_CONFIG_MACRO_DIRS([m4-gen])

dnl Consumed by doc/api/main_page.dox.in.
AC_SUBST_FILE([LICENSE_FILE])
LICENSE_FILE=${srcdir}/LICENSE

# We don't want to use the cflags from the environment since we need control
# of this when we're building the libs.
CFLAGS=""


dnl This fix is from Newlib in config/override.m4.
dnl Fix 2.64 cross compile detection for AVR by not trying to compile fopen.
m4_if(m4_defn([m4_PACKAGE_VERSION]), [2.64],
  [m4_foreach([_GCC_LANG], [C, C++, Fortran, Fortran 77],
     [m4_define([_AC_LANG_IO_PROGRAM(]_GCC_LANG[)], m4_defn([AC_LANG_PROGRAM(]_GCC_LANG[)]))])])


# Checks for programs.

AC_PROG_CC
AC_CHECK_TOOL(AS, as, as)
AC_CHECK_TOOL(LD, ld, ld)
AM_PROG_AS
AC_PROG_RANLIB
AC_CHECK_TOOL(AR, ar, ar)
AC_CHECK_TOOL(OBJDUMP, objdump, objdump)

AC_MSG_CHECKING([if the build compiler is GCC 4.8 or newer])
AS_VERSION_COMPARE([`${CC} -dumpversion`], [4.8.0],
    [AC_MSG_RESULT(no)
     AC_MSG_ERROR(AVR-LibC must be built with GCC 4.8 or newer.)],
    [AC_MSG_RESULT(yes)],
    [AC_MSG_RESULT(yes)])

gcc_major1=`${CC} -dumpversion | cut -d '.' -f 1`
gcc_major2=`${CC} -dumpversion | cut -d '.' -f 1,2`
AS_VERSION_COMPARE([`${CC} -dumpversion`], [5.0.0],
    [AC_SUBST(GCC_MAJOR, [$gcc_major2])],
    [AC_SUBST(GCC_MAJOR, [$gcc_major1])],
    [AC_SUBST(GCC_MAJOR, [$gcc_major1])])

dnl Parts of the documentaton like in doc/api require Python, e.g. math2dox.
dnl We cannot use devtools' configure since that one runs at bootstrap time,
dnl whereas here we need Python at build time.
AM_PATH_PYTHON([2.6],
    [AC_MSG_NOTICE([using $PYTHON])],
    dnl Python is only needed for the documentation, so relax
    dnl when no Python is found.
    [:])
AM_CONDITIONAL([HAVE_PYTHON], [test "$PYTHON" != :])

AC_PROG_AWK
AC_PROG_GREP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_SED

# Show the tool versions in the configure log and in the FAQ.

SUBST_VERSION_STRING([CC_VERSION_STRING], [$CC ])
SUBST_VERSION_STRING([AS_VERSION_STRING], [$AS ])
SUBST_VERSION_STRING([LD_VERSION_STRING], [$LD ])
SUBST_VERSION_STRING([CCLD_VERSION_STRING], [echo "" | $CC -xassembler - -Wl,])

AC_MSG_NOTICE([Using CC=$CC: $CC_VERSION_STRING])
AC_MSG_NOTICE([Using AS=$AS: $AS_VERSION_STRING])
AC_MSG_NOTICE([Using LD=$LD: $LD_VERSION_STRING])

dnl Issue a warning when CC's linker is not LD.
AS_IF([test "x$LD_VERSION_STRING" != "x$CCLD_VERSION_STRING"],[dnl
    AC_MSG_NOTICE([Using CC linker: $CCLD_VERSION_STRING])
    AC_MSG_WARN([different linkers used in CC and LD])
],[])


# Make sure that we found the right avr cross-compiler.

case "${CC}" in
   *gcc*)
       case "X`${CC} -dumpmachine`X" in
           XavrX) ;;
           *) AC_MSG_ERROR(Wrong C compiler found; check the PATH!) ;;
       esac
       ;;
   *) AC_MSG_ERROR(Wrong C compiler found; check the PATH!) ;;
esac
case "${AS}" in
   *avr*as*) ;;
   *) AC_MSG_ERROR(Wrong assembler found; check the PATH!) ;;
esac
case "${LD}" in
   *avr*ld*) ;;
   *) AC_MSG_ERROR(Wrong linker found; check the PATH!) ;;
esac
case "${OBJDUMP}" in
   *avr*objdump*) ;;
   *) AC_MSG_ERROR(Wrong objdump found; check the PATH!) ;;
esac
case "${AR}" in
   *avr*ar*) ;;
   *) AC_MSG_ERROR(Wrong archiver found; check the PATH!) ;;
esac
case "${RANLIB}" in
   *avr*ranlib*) ;;
   *) AC_MSG_ERROR(Wrong ranlib found; check the PATH!) ;;
esac

dnl Outcome is consumed by crt1/gcrt1.S
CHECK_OBJDUMP_MINUS_H([whether data region starts at __DATA_REGION_ORIGIN__],dnl
    [.data],dnl
    [-mmcu=avr2], [-mavr2 --defsym __DATA_REGION_ORIGIN__=0x800066],
    ['\.data.*00800066'],
    [ HAVE_LDSCRIPT_PR29741_yesno=yes
      AC_DEFINE([HAVE_LDSCRIPT_PR29741], 1,
        [Define if the ld script uses __DATA_REGION_ORIGIN__ as data start.]) ],
    [ HAVE_LDSCRIPT_PR29741_yesno=no ]
)
AC_SUBST(HAVE_LDSCRIPT_PR29741_yesno)

dnl Outcome is consumed by crt1/gcrt1.S
CHECK_OBJDUMP_MINUS_H([whether text region starts at __TEXT_REGION_ORIGIN__],dnl
    [.data],dnl
    [-mmcu=avr2], [-mavr2 --defsym __TEXT_REGION_ORIGIN__=0x22],
    ['\.text.*00000022'],
    [ HAVE_LDSCRIPT_PR31177_yesno=yes
      AC_DEFINE([HAVE_LDSCRIPT_PR31177], 1,
        [Define if the ld script uses __TEXT_REGION_ORIGIN__ as text start.]) ],
    [ HAVE_LDSCRIPT_PR31177_yesno=no ]
)
AC_SUBST(HAVE_LDSCRIPT_PR31177_yesno)

dnl Build a list of all .h files in  $srcdir/include/avr.
dnl Used by include/avr/Makefile.am::avr_HEADERS.
SUBST_BASENAME_FILES(AVRLIBC_INCLUDE_HFILES, [$srcdir/include/avr/*.h])

dnl Build a list of all .S files in  $srcdir/crt1/iosym.
dnl Used by crt1/iosym/Makefile.am::EXTRA_DIST.
SUBST_BASENAME_FILES(AVRLIBC_IOSYM_SFILES, [$srcdir/crt1/iosym/*.S])

dnl Build a list of all .m4 files in  $srcdir/m4.
dnl Used by m4/Makefile.am::EXTRA_DIST.
SUBST_BASENAME_FILES(AVRLIBC_M4_M4FILES, [$srcdir/m4/*.m4])

dnl Whether to build the lib<mcu>.a device libraries.
dnl Does AC_SUBST(HAS_DEV_LIB_yesno)
dnl Does AM_CONDITIONAL HAS_GCC_5_1 and HAS_DEV_LIB.
CHECK_DEVICE_LIB

dnl Does AC_SUBST(FNO_JUMP_TABLES).
CHECK_JUMP_TABLES_ISSUE

dnl Does AC_SUBST(HAS_DELAY_CYCLES).
CHECK_BUILTIN_DELAY_CYCLES

CHECK_CC_OPTION_AND_SET([-Wno-maybe-uninitialized], [WNO_MAYBE_UNINITIALIZED])
AC_SUBST(WNO_MAYBE_UNINITIALIZED)

AC_MSG_CHECKING([whether ${CC} supports -mcvt])
DO_IF_CC_OPTION([-mcvt], [HAS_MCVT_yesno=yes], [HAS_MCVT_yesno=no])
AC_MSG_RESULT([$HAS_MCVT_yesno])
AC_SUBST(HAS_MCVT_yesno)

AC_MSG_CHECKING([whether ${CC} supports attribute signal(n)])
DO_IF_CC_BUILTIN_MACRO([], [__HAVE_SIGNAL_N__],
	[HAS_SIGNAL_N_yesno=yes], [HAS_SIGNAL_N_yesno=no])
AC_MSG_RESULT([$HAS_SIGNAL_N_yesno])
AC_SUBST(HAS_SIGNAL_N_yesno)

dnl Does AM_CONDITIONAL(BUILD_FXLIB, ...)
CHECK_FIXED_POINT


dnl == Documentation =========================================================

dnl TODO: Check for various tools used to build the documentation. If anything
dnl is missing, don't build the docs by default.

dnl Disable building of dox by default since they may not build properly on
dnl all systems. This isn't optimal, but how do we work around the user not
dnl having fig2dev and still build all the formats?

AC_ARG_ENABLE(doc,
[AS_HELP_STRING([--enable-doc],[build all doc formats (disabled is default)])],
[case "${enableval}" in
  yes) enable_doc=yes ;;
  no)  enable_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for global doc option) ;;
 esac], [enable_doc=no])dnl
dnl Various documentation options.  By default, we enable all of them,
dnl i. e. HTML, Postscript, PDF, and Unix-style man pages.
AC_ARG_ENABLE(html-doc,
[AS_HELP_STRING([--enable-html-doc],[build HTML documentation (default)])],
[case "${enableval}" in
  yes) html_doc=yes ;;
  no)  html_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for html-doc option) ;;
 esac], [html_doc=yes])dnl

AC_ARG_ENABLE(pdf-doc,
[AS_HELP_STRING([--enable-pdf-doc],[build PDF documentation (default)])],
[case "${enableval}" in
  yes) pdf_doc=yes ;;
  no)  pdf_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for pdf-doc option) ;;
 esac], [pdf_doc=yes])dnl

dnl Man pages are currently not tunable; they'll be built whenever
dnl the HTML documentation is built.  However, it can be configured
dnl whether the result is about to be installed or not.
AC_ARG_ENABLE(man-doc,
[AS_HELP_STRING([--enable-man-doc],[build Unix-style manual pages (default)])],
[case "${enableval}" in
  yes) man_doc=yes ;;
  no)  man_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for man-doc option) ;;
 esac], [man_doc=yes])dnl

AC_ARG_ENABLE(versioned-doc,
[AS_HELP_STRING([--enable-versioned-doc],
		[install docs in directory with version name (default)])],
[case "${enableval}" in
  yes) versioned_doc=yes ;;
  no)  versioned_doc=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for versioned-doc option) ;;
 esac], [versioned_doc=yes])dnl


CHECK_DOXYGEN

if test "$versioned_doc" = "yes"; then
	DOC_INST_DIR='${DESTDIR}${datadir}/doc/avr-libc-$(VERSION)'
	AVR_LIBC_USER_MANUAL="avr-libc-user-manual-${VERSION}"
else
	DOC_INST_DIR='${DESTDIR}${datadir}/doc/avr-libc'
	AVR_LIBC_USER_MANUAL="avr-libc-user-manual"
fi

dnl We only want to build the docs if the user has passed
dnl "--enable-doc" to configure.
if test "$enable_doc" = "yes"; then
	DOCSDIR='api'
else
	DOCSDIR=''
fi

AC_SUBST(DOCSDIR)
AC_SUBST(AVR_LIBC_USER_MANUAL)
AC_SUBST(DOC_INST_DIR)

dnl Used in doc/api's doxygen.config.in::MAN_EXTENSION and Makefile.am.
AC_ARG_WITH([man-page],
  [AS_HELP_STRING([--with-man-page=],dnl
                  [set man page for AVR-LibC documentation (default: 3avr)])],
  [MAN_PAGE=$withval],
  [MAN_PAGE=3avr])
AC_MSG_NOTICE([Using man page $MAN_PAGE])
AC_SUBST(MAN_PAGE)

dnl Consumed by doc/api/doxygen.config.in::ENABLED_SECTIONS.
AC_MSG_CHECKING([for git branch])
the_git_branch=`(cd ${srcdir}; git rev-parse --abbrev-ref HEAD 2> /dev/null)`
AS_IF([test "x$the_git_branch" != "x"],
      [AC_MSG_RESULT([$the_git_branch])],
      [AC_MSG_RESULT([--none--])])
AS_IF([echo "$the_git_branch" | $GREP onlinedocs > /dev/null 2>&1],
      [GIT_BRANCH_IS_ONLINEDOCS=GIT_BRANCH_IS_ONLINEDOCS],
      [GIT_BRANCH_IS_ONLINEDOCS=])
AC_SUBST(GIT_BRANCH_IS_ONLINEDOCS)


dnl == Debug Info ============================================================

dnl Let the user decide which debug information to generate.
dnl Default is nothing, suitable for binary distributions of the
dnl compiled library.  Alternative options include stabs, dwarf-2,
dnl dwarf-4 or [compiler's default version] dwarf.
AC_ARG_ENABLE(debug-info,
[AS_HELP_STRING([--enable-debug-info=stabs|dwarf-2|dwarf-4],
		[Enable generation of debugging information])],
[case "${enableval}" in
  yes|dwarf2|dwarf-2) debuginfo=dwarf2 ;;
  no)                 debuginfo="" ;;
  stabs)              debuginfo=stabs ;;
  dwarf4|dwarf-4)     debuginfo=dwarf4 ;;
  dwarf)              debuginfo=dwarf ;;
  *)                  AC_MSG_ERROR([bad value ${enableval} for --enable-debug-info option; should be either stabs, dwarf-2, or dwarf-4]) ;;
 esac], [debuginfo=""])

dnl Decide which debuginfo information to include
case "$debuginfo" in
"")      CDEBUG=""; ASDEBUG="" ;;
stabs)   CDEBUG="-gstabs"; ASDEBUG="-Wa,-gstabs" ;;
dwarf2)  CDEBUG="-gdwarf-2"; ASDEBUG="-Wa,-gdwarf-2" ;;
dwarf4)  CDEBUG="-gdwarf-4"; ASDEBUG="-Wa,-gdwarf-2" ;;
dwarf)   CDEBUG="-gdwarf"; ASDEBUG="-Wa,-gdwarf-2" ;;
esac

AC_SUBST(CDEBUG)
AC_SUBST(ASDEBUG)


dnl == Devices and Multilibs =================================================

dnl The rest of the file deals with the files as generated by mlib-gen.py
dnl during bootstrap.  Macros are defined that are used by the m4 files
dnl located in m4-gen.

AC_COMPILE_IFELSE([AC_LANG_SOURCE([[
	#if !defined(__SIZEOF_LONG_DOUBLE__) || __SIZEOF_LONG_DOUBLE__ != 8
	int a[-1];
	#endif
	]],[])],
	[DOX_MULTI_OPT=yes],)

# Used by Device.am i.e. avr/devices/<device>/Makefile.am.
CHECK_MULTI_OPTION([-mdouble=32], [double32], [double32])
CHECK_MULTI_OPTION([-mdouble=64], [double64], [double64])
CHECK_MULTI_OPTION([-mlong-double=32], [long_double32], [long-double32])
CHECK_MULTI_OPTION([-mlong-double=64], [long_double64], [long-double64])

AS_IF([test "x$DOX_MULTI_OPT" = "x"],
      [DOX_MULTI_OPT=no],
      [DOX_MULTI_OPT=`echo "$DOX_MULTI_OPT" | $SED 's/, /: /'`])
AC_SUBST(DOX_MULTI_OPT)

# avr1
AM_CONDITIONAL(HAS_avr1, true)
DIR_avr1=avr1
AC_SUBST(DIR_avr1)

dnl Will be worked out by CHECK_AVR_DEVICE.
COND_DEFINED_Dx="0"
COND_DEFINED_Ex="0"
COND_DEFINED_Lx="0"
COND_DEFINED_Sx="0"
SUPPORTED_MCUS=""

dnl Will be worked out by CHECK_AVR_RESERVED.
COND_BAUD_RESERVED="0"

dnl Runs CHECK_AVR_DEVICE, CHECK_AVR_CVT, CHECK_AVR_RESERVED for all MCUs.
m4_include([m4-gen/devices.m4])

AC_SUBST(SUPPORTED_MCUS)
AC_SUBST(COND_DEFINED_Dx)
AC_SUBST(COND_DEFINED_Ex)
AC_SUBST(COND_DEFINED_Lx)
AC_SUBST(COND_DEFINED_Sx)
AC_SUBST(COND_BAUD_RESERVED)

dnl The following include must precede multilib.m4 and is only
dnl needed with avr-gcc v4.9 or older, namely if !HAS_DEV_LIB.
dnl Runs AC_SUBST(DEVLIST_<mlib>).
m4_include([m4-gen/devlist.m4])

dnl Runs CHECK_MULTI_VARIANT for all multilib variants.
m4_include([m4-gen/multilib.m4])


dnl == Generate all files from *.in sources  =================================

dnl The *.in scripts in devtools are transformed by the local configure.
AC_CONFIG_FILES([
	supported-mcus.txt
	include/avr/builtins.h
	include/avr/version.h
	include/util/delay.h
	doc/api/doxygen.config
	doc/api/main_page.dox
	doc/api/faq.dox
])

AC_CONFIG_FILES([scripts/avr-man], [chmod +x scripts/avr-man])
AC_CONFIG_FILES([devtools/math2dox], [chmod +x devtools/math2dox])
AC_CONFIG_FILES([doc/api/filter-dox.sh], [chmod +x doc/api/filter-dox.sh])

dnl Makefile.in's made by automake from Makefile.am's.
AC_CONFIG_FILES([
	Makefile
	common/Makefile
	common/f7/Makefile
	crt1/Makefile
	crt1/iosym/Makefile
	doc/Makefile
	doc/api/Makefile
	doc/examples/Makefile
	include/Makefile
	include/avr/Makefile
	include/bits/Makefile
	include/bits/devinfo.h
	include/compat/Makefile
	include/util/Makefile
	include/sys/Makefile
	libc/Makefile
	libc/misc/Makefile
	libc/pmstring/Makefile
	libc/stdio/Makefile
	libc/stdlib/Makefile
	libc/string/Makefile
	libc/time/Makefile
	libm/Makefile
	libm/fplib/Makefile
	libm/fxlib/Makefile
	m4/Makefile
	scripts/Makefile
	devtools/Makefile
])

dnl Runs AC_CONFIG_FILES on all Makefile.am's from mlib-gen.py.
m4_include([m4-gen/files.m4])

AC_OUTPUT
